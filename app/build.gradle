apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.jakewharton.hugo'

ext.languages = ["de", "es", "fa", "fr", "in", "it", "ja", "pl", "ru", "tr", "zh-rCN", "zh-rTW"]
ext.supportLibraryVersion = "26.0.2"
ext.c_languages = ["", "es-ES", "fa", "fr", "id", "it", "ja", "pl", "ru", "tr", "zh-CN", "zh-TW"]
ext.crowdinKey = null

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.0'
    defaultConfig {
        applicationId "de.Maxr1998.xposed.maxlock"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 45
        versionName "6.4.1"
        resConfigs languages
        setProperty("archivesBaseName", "ML-$versionName")

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            versionNameSuffix "-beta"
        }
    }
    sourceSets {
        main {
            res.srcDirs = [
                    res.srcDirs.toArray()[0],
                    "src/main/res/translations"
            ]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
}

def propFile = project.file("keyStore.properties")
if (propFile.exists()) {
    Properties props = new Properties()
    props.load(new FileInputStream(propFile))
    def main = android.signingConfigs.create("main")
    main.storeFile project.file(props["keyStore"])
    main.storePassword props["keyStorePassword"]
    main.keyAlias props["keyAlias"]
    main.keyPassword props["keyPassword"]

    android.buildTypes.release.signingConfig = main
    android.buildTypes.debug.signingConfig = main

    ext.crowdinKey = props["crowdinKey"]
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    // Kotlin support
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    // From JCenter or other repositories
    //-- 3rd-party
    compile 'com.anjlab.android.iab.v3:library:1.0.38@aar'
    compile 'com.crossbowffs.remotepreferences:remotepreferences:0.5'
    compile 'com.github.attenzione:android-ColorPickerPreference:e3aa301016'
    compile 'com.github.shkschneider:android_PreferenceFragmentCompat:1.0.0@aar'
    compile 'com.google.guava:guava:22.0-android'
    compile 'com.heinrichreimersoftware:material-intro:1.6'
    compile 'com.simplecityapps:recyclerview-fastscroll:1.0.5'
    compile 'com.twofortyfouram:android-plugin-api-for-locale:1.0.2'
    compile 'commons-io:commons-io:2.4'
    provided 'de.robv.android.xposed:api:82'
    provided 'de.robv.android.xposed:api:82:sources'
    //-- Android support and Google Services
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:customtabs:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:gridlayout-v7:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    /* includes all other support-v4 libraries */
    compile "com.android.support:support-fragment:$supportLibraryVersion"
    compile 'com.google.android.gms:play-services-analytics:11.2.2'
    // Local files
    compile(name: 'PatternLock', ext: 'aar')
}

task updateTranslations() {
    doLast {
        if (crowdinKey == null)
            return
        for (int i = 0; i < c_languages.size(); i++) {
            String c_lang = c_languages[i]
            if (c_lang == "")
                continue
            String lang = languages[i]
            String file = "\$PWD/src/main/res/translations/values-$lang/strings.xml"
            exec {
                executable "bash"
                args "-c", "wget -O \"$file\" \"https://api.crowdin.com/api/project/MaxLock/export-file?file=strings.xml&language=$c_lang&key=$crowdinKey\""
            }
        }
    }
}