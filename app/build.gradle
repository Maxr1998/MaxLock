apply plugin: 'com.android.application'
android {
    signingConfigs {
        main
    }
    compileSdkVersion 23
    buildToolsVersion '23.0.2'
    defaultConfig {
        applicationId "de.Maxr1998.xposed.maxlock"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 36
        versionName "5.3.2"
    }
    buildTypes {
        release {
            debuggable false
            signingConfig signingConfigs.main
        }
        debug {
            debuggable true
            signingConfig signingConfigs.main
            versionNameSuffix "-beta"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        abortOnError false
    }
}

def propFile = project.file("keyStore.properties")
if (propFile.exists()) {
    def props = new Properties()
    props.load(new FileInputStream(propFile))
    android.signingConfigs.main.storeFile project.file(props.keyStore)
    android.signingConfigs.main.storePassword props.keyStorePassword
    android.signingConfigs.main.keyAlias props.keyAlias
    android.signingConfigs.main.keyPassword props.keyPassword
}

repositories {
    jcenter()
    maven {
        url "https://jitpack.io"
    }
    flatDir {
        dirs 'libs'
    }
}
dependencies {
    // From Maven or other repositories
    //-- 3rd-party
    compile 'com.anjlab.android.iab.v3:library:1.0.27@aar'
    compile 'com.github.attenzione:android-ColorPickerPreference:e3aa301016'
    compile 'com.github.shkschneider:android_PreferenceFragmentCompat:1.0.0@aar'
    compile 'com.google.guava:guava:19.0-rc1'
    compile 'commons-io:commons-io:2.4'
    compile 'xyz.danoz:recyclerviewfastscroller:0.1.3'
    //-- Android support and Google Services
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:customtabs:23.1.0'
    compile 'com.android.support:palette-v7:23.1.0'
    compile 'com.android.support:recyclerview-v7:23.1.0'
    compile 'com.android.support:support-v4:23.1.0'
    compile 'com.google.android.gms:play-services-base:8.3.0'
    compile 'com.google.android.gms:play-services-analytics:8.3.0'
    // Local files
    compile(name: 'PatternLock', ext: 'aar')
    provided files('lib/XposedBridgeApi-20150213.jar')

    //debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    //releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
}